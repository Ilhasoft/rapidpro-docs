## Expression Syntaxes
 
Understand what is it and how to use available functions and expressions.
 
Expressions are modeled within specific syntaxes and starting with an `@` will load a list of 10 possibilities, you can advance to complex expressions by typing `@(` - called *Functions*.
 
`@contact` â†’ The name of the contact. </br>
`@results` â†’ The results for the current run. </br>
`@fields` â†’ The custom field for the contact. </br>
`@input` â†’ The most rescent input. </br>
`@urns` â†’ The URNs for the contact. </br>
`@run` â†’ A run in this flow. </br>
`@child` â†’ Run details collected in a child flow, if any. </br>
`@parent` â†’ Run details collected in a parent flow, if any. </br>
`@webhook` â†’ The parsed JSON payload of the last webhook call. </br>
`@trigger` â†’ A trigger that initiated a session. </br>
 
![](/img/expressions/ExpressionDetails01.png)
 
## Simple Syntaxes
 
In some cases it's necessary only to call single values, for example:
 
- Hello `@contact`, have you accessed the `@result.support`, is it right?
 
The expression can also be called with some function, for example:
 
- Hello `@(title(contact.first_name))`.
 
Functions' names do not differ in Upper or Lower case. On the example above, using `title` will return the first letter of contact's first name in the upper case.
 
## Advanced Syntaxes
 
There are cases when building more complex expressions will be necessary and calling multiple expressions will interpret a final result needed, for example:
 
- Hello `@(title(contact.first_name))`. You are `@(YEAR(NOW()) - result.year_born)` years old, am I right?
 
Math expressions are included:
 
- Addition `+`
- Subtraction `-`
- Multiplication `*`
- Division `/`
- Powers (Exponential) `^`
 
**The result is @((1+(1+2)*2^3)/5).**
 
Notice that, the expression between parenthesis sets where the expression must finish.
 
You can also concatenate operators `&`, for example: Hello `@(contact.first_name & " " & contact.last_name)`.

## Expression's and Functions' types

This is a full list of all available features of the platform.
*Function* arguments into brackets `([â€¦])` are optional. But notice, when replacing variables the quotation marks are necessary. e.g.: `@(format_time(now()))`

### **array**

  Is an array of items.
```objectivec
@(array(1, "x", true)) â†’ [1, x, true]
@(array(1, "x", true)[1]) â†’ x
@(count(array(1, "x", true))) â†’ 3
@(json(array(1, "x", true))) â†’ [1,"x",true]
```

### **boolean**

  Is a boolean `true` or `false`.

```objectivec
@(true) â†’ true
@(1 = 1) â†’ true
@(1 = 2) â†’ false
@(json(true)) â†’ true
```

### **date**

  Is a Gregorian calendar date value.
```objectivec
@(date_from_parts(2019, 4, 11)) â†’ 2019-04-11
@(format_date(date_from_parts(2019, 4, 11))) â†’ 11-04-2019
@(json(date_from_parts(2019, 4, 11))) â†’ "2019-04-11"
```

### **datetime**

  Is a datetime value.
```objectivec
@(datetime("1979-07-18T10:30:45.123456Z")) â†’ 1979-07-18T10:30:45.123456Z
@(format_datetime(datetime("1979-07-18T10:30:45.123456Z"))) â†’ 18-07-1979 05:30
@(json(datetime("1979-07-18T10:30:45.123456Z"))) â†’ "1979-07-18T10:30:45.123456Z"
```

### **function**

  Is a callable function.
```objectivec
@(upper) â†’ function
@(array(upper)[0]("abc")) â†’ ABC
@(json(upper)) â†’ null
```

### **Number**

  Is a whole or fractional number.
```objectivec
@(1234) â†’ 1234
@(1234.5678) â†’ 1234.5678
@(format_number(1234.5670)) â†’ 1,234.567
@(json(1234.5678)) â†’ 1234.5678
```

### **object**

  Is an object with named properties.
```objectivec
@(object("foo", 1, "bar", "x")) â†’ {bar: x, foo: 1}
@(object("foo", 1, "bar", "x").bar) â†’ x
@(object("foo", 1, "bar", "x")["bar"]) â†’ x
@(count(object("foo", 1, "bar", "x"))) â†’ 2
@(json(object("foo", 1, "bar", "x"))) â†’ {"bar":"x","foo":1}
```

### **text**

  Is a string of characters.
```objectivec
@("abc") â†’ abc
@(text_length("abc")) â†’ 3
@(upper("abc")) â†’ ABC
@(json("abc")) â†’ "abc"
```

### **time**

  Is a time of day.
```objectivec
@(time_from_parts(16, 30, 45)) â†’ 16:30:45.000000
@(format_time(time_from_parts(16, 30, 45))) â†’ 16:30
@(json(time_from_parts(16, 30, 45))) â†’ "16:30:45.000000"
```

## **Operators' types**

### **add**

  Adds two numbers.
```objectivec
@(2 + 3) â†’ 5
@(fields.age + 10) â†’ 33
```

### **concatenate**

  Joins two text values together.
```objectivec
@("hello" & " " & "bar") â†’ hello bar
@("hello" & null) â†’ hello
```

### **divide**

  Divides a number by another.
```objectivec
@(4 / 2) â†’ 2
@(3 / 2) â†’ 1.5
@(46 / fields.age) â†’ 2
@(3 / 0) â†’ ERROR
```

### **equal**

  Returns true if two values are textually equal.
```objectivec
@("hello" = "hello") â†’ true
@("hello" = "bar") â†’ false
@(1 = 1) â†’ true
```
### **exponent**

  Raises a number to the power of another number.
```objectivec
@(2 ^ 8) â†’ 256
```
### **greaterthan**

  Returns true if the first number is greater than the second.
```objectivec
@(2 > 3) â†’ false
@(3 > 3) â†’ false
@(4 > 3) â†’ true
```

### **greaterthanorequal**

  Returns true if the first number is greater than or equal to the second.
```objectivec
@(2 >= 3) â†’ false
@(3 >= 3) â†’ true
@(4 >= 3) â†’ true
```

### **lessthan**

  Returns true if the first number is less than the second.
```objectivec
@(2 < 3) â†’ true
@(3 < 3) â†’ false
@(4 < 3) â†’ false
```

### **lessthanorequal**

  Returns true if the first number is less than or equal to the second.
```objectivec
@(2 <= 3) â†’ true
@(3 <= 3) â†’ true
@(4 <= 3) â†’ false
```

### **multiply**

  Multiplies two numbers.
```objectivec
@(3 * 2) â†’ 6
@(fields.age * 3) â†’ 69
```

### **negate**

  Negates a number
```objectivec
@(-fields.age) â†’ -23
```
### ***notequal***

  Returns true if two values are textually not equal.
```objectivec
@("hello" != "hello") â†’ false
@("hello" != "bar") â†’ true
@(1 != 2) â†’ true
```

### **subtract**

  Subtracts two numbers.
```objectivec
@(3 - 2) â†’ 1
@(2 - 3) â†’ -1
```

## Function's list

### **abs(number)**

  Returns the absolute value of `number`.
```objectivec
@(abs(-10)) â†’ 10
@(abs(10.5)) â†’ 10.5
@(abs("foo")) â†’ ERROR
```
### **and(values...)**
  
  Returns whether all the given `values` are truthy.
```objectivec
@(and(true)) â†’ true
@(and(true, false, true)) â†’ false
```
### **array(values...)**
  
  Takes multiple `values` and returns them as an array.
```objectivec
@(array("a", "b", 356)[1]) â†’ b
@(join(array("a", "b", "c"), "|")) â†’ a|b|c
@(count(array())) â†’ 0
@(count(array("a", "b"))) â†’ 2
```
### **attachment_parts(attachment)**
  
  Parses an attachment into its different parts.
```objectivec
@(attachment_parts("image/jpeg:https://example.com/test.jpg")) â†’ {content_type: image/jpeg, url: https://example.com/test.jpg}
```
### **boolean(value)**
  
  Tries to convert `value` to a boolean.

  An error is returned if the value can't be converted.
```objectivec
@(boolean(array(1, 2))) â†’ true
@(boolean("FALSE")) â†’ false
@(boolean(1 / 0)) â†’ ERROR
```
### **char(code)**
  
  Returns the character for the given UNICODE `code`.

  It is the inverse of code.
```objectivec
@(char(33)) â†’ !
@(char(128512)) â†’ ğŸ˜€
@(char("foo")) â†’ ERROR
```
### **clean(text)**
  
  Removes any non-printable characters from `text`.
```objectivec
@(clean("ğŸ˜ƒ Hello \nwo\tr\rld")) â†’ ğŸ˜ƒ Hello world
@(clean(123)) â†’ 123
```
### **code(text)**
  
  Returns the UNICODE code for the first character of `text`.

  It is the inverse of char.
```objectivec
@(code("a")) â†’ 97
@(code("abc")) â†’ 97
@(code("ğŸ˜€")) â†’ 128512
@(code("15")) â†’ 49
@(code(15)) â†’ 49
@(code("")) â†’ ERROR
```
### **count(value)**
  
  Returns the number of items in the given array or the properties of an object.

  It will return an error if it is passed an item which isn't countable.
```objectivec
@(count(contact.fields)) â†’ 5
@(count(array())) â†’ 0
@(count(array("a", "b", "c"))) â†’ 3
@(count(1234)) â†’ ERROR
```
### **date(value)**
  
  Tries to convert `value` to a date.
  
  If it is text then it will be parsed into a date using the default date format. An error is returned if the value can't be converted.
```objectivec
@(date("1979-07-18")) â†’ 1979-07-18
@(date("1979-07-18T10:30:45.123456Z")) â†’ 1979-07-18
@(date("10/05/2010")) â†’ 2010-05-10
@(date("NOT DATE")) â†’ ERROR
```
### **date_from_parts(year, month, day)**
  
  Creates a date from `year`, `month` and `day`.
```objectivec
@(date_from_parts(2017, 1, 15)) â†’ 2017-01-15
@(date_from_parts(2017, 2, 31)) â†’ 2017-03-03
@(date_from_parts(2017, 13, 15)) â†’ ERROR
```
### **datetime(value)**
  
  Tries to convert `value` to a datetime.

  If it is text then it will be parsed into a datetime using the default date and time formats. An error is returned if the value can't be converted.
```objectivec
@(datetime("1979-07-18")) â†’ 1979-07-18T00:00:00.000000-05:00
@(datetime("1979-07-18T10:30:45.123456Z")) â†’ 1979-07-18T10:30:45.123456Z
@(datetime("10/05/2010")) â†’ 2010-05-10T00:00:00.000000-05:00
@(datetime("NOT DATE")) â†’ ERROR
```
### **datetime_add(datetime, offset, unit)**
  
  Calculates the date value arrived at by adding `offset` number of `unit` to the `datetime`.

  Valid durations are `Y` for years, `M` for months, `W` for weeks, `D` for days, `h` for hour, `m` for minutes, `s` for seconds.
```objectivec
@(datetime_add("2017-01-15", 5, "D")) â†’ 2017-01-20T00:00:00.000000-05:00
@(datetime_add("2017-01-15 10:45", 30, "m")) â†’ 2017-01-15T11:15:00.000000-05:00
```
### **datetime_diff(date1, date2, unit)**
  
  Returns the duration between `date1` and `date2` in the `unit` specified.
  Valid durations are `Y` for years, `M` for months, `W` for weeks, `D` for days, `h` for hour, `m` for minutes, `s` for seconds.
```objectivec
@(datetime_diff("2017-01-15", "2017-01-17", "D")) â†’ 2
@(datetime_diff("2017-01-15", "2017-05-15", "W")) â†’ 17
@(datetime_diff("2017-01-15", "2017-05-15", "M")) â†’ 4
@(datetime_diff("2017-01-17 10:50", "2017-01-17 12:30", "h")) â†’ 1
@(datetime_diff("2017-01-17", "2015-12-17", "Y")) â†’ -2
```
### **datetime_from_epoch(seconds)**
  
  Converts the UNIX epoch time `seconds` into a new date.
```objectivec
@(datetime_from_epoch(1497286619)) â†’ 2017-06-12T11:56:59.000000-05:00
@(datetime_from_epoch(1497286619.123456)) â†’ 2017-06-12T11:56:59.123456-05:00
```
### **default(value, default)**
  
  Returns `value` if is not empty or an error, otherwise it returns `default`.
```objectivec
@(default(undeclared.var, "default_value")) â†’ default_value
@(default("10", "20")) â†’ 10
@(default("", "value")) â†’ value
@(default(array(1, 2), "value")) â†’ [1, 2]
@(default(array(), "value")) â†’ value
@(default(datetime("invalid-date"), "today")) â†’ today
@(default(format_urn("invalid-urn"), "ok")) â†’ ok
```
### **epoch(date)**
  
  Converts `date` to a UNIX epoch time.

  The returned number can contain fractional seconds.
```objectivec
@(epoch("2017-06-12T16:56:59.000000Z")) â†’ 1497286619
@(epoch("2017-06-12T18:56:59.000000+02:00")) â†’ 1497286619
@(epoch("2017-06-12T16:56:59.123456Z")) â†’ 1497286619.123456
@(round_down(epoch("2017-06-12T16:56:59.123456Z"))) â†’ 1497286619
```
### **extract(object, properties)**
  
  Takes an object and extracts the named property.
```objectivec
@(extract(contact, "name")) â†’ Ryan Lewis
@(extract(contact.groups[0], "name")) â†’ Testers
```
### **extract_object(object, properties...)**
  
  Takes an object and returns a new object by extracting only the named properties.
```objectivec
@(extract_object(contact.groups[0], "name")) â†’ {name: Testers}
```
### **field(text, index, delimiter)**
  
  Splits `text` using the given `delimiter` and returns the field at `index`.

  The index starts at zero. When splitting with a space, the delimiter is considered to be all whitespace.
```objectivec
@(field("a,b,c", 1, ",")) â†’ b
@(field("a,,b,c", 1, ",")) â†’ 
@(field("a   b c", 1, " ")) â†’ b
@(field("a      b   c   d", 1, "    ")) â†’ 
@(field("a\t\tb\tc\td", 1, " ")) â†’ 
@(field("a,b,c", "foo", ",")) â†’ ERROR
```
### **foreach(values, func, [args...])**
  
  Creates a new array by applying `func` to each value in `values`.

  If the given function takes more than one argument, you can pass additional arguments after the function.
```objectivec
@(foreach(array("a", "b", "c"), upper)) â†’ [A, B, C]
@(foreach(array("the man", "fox", "jumped up"), word, 0)) â†’ [the, fox, jumped]
```
### **foreach_value(object, func, [args...])**
  
  Creates a new object by applying `func` to each property value of `object`.

  If the given function takes more than one argument, you can pass additional arguments after the function.
```objectivec
@(foreach_value(object("a", "x", "b", "y"), upper)) â†’ {a: X, b: Y}
@(foreach_value(object("a", "hi there", "b", "good bye"), word, 1)) â†’ {a: there, b: bye}
```
### **format(value)**
  
  Formats `value` according to its type.
```objectivec
@(format(1234.5670)) â†’ 1,234.567
@(format(now())) â†’ 11-04-2018 13:24
@(format(today())) â†’ 11-04-2018
```
### **format_date(date,, [format])**
  
  Formats `date` as text according to the given `format`.

  If `format` is not specified then the environment's default format is used. The format string can consist of the following characters. Characters like *space*, `:`, `,`, `T`, `-` and `_` are ignored. Any other character is an error.

  * `YY` - last two digits of year 0-99
  * `YYYY` - four digits of year 0000-9999
  * `M` - month 1-12
  * `MM` - month 01-12
  * `D` - day of month, 1-31
  * `DD` - day of month, zero padded 0-31
```objectivec
@(format_date("1979-07-18T15:00:00.000000Z")) â†’ 18-07-1979
@(format_date("1979-07-18T15:00:00.000000Z", "YYYY-MM-DD")) â†’ 1979-07-18
@(format_date("2010-05-10T19:50:00.000000Z", "YYYY M DD")) â†’ 2010 5 10
@(format_date("1979-07-18T15:00:00.000000Z", "YYYY")) â†’ 1979
@(format_date("1979-07-18T15:00:00.000000Z", "M")) â†’ 7
@(format_date("NOT DATE", "YYYY-MM-DD")) â†’ ERROR
```
### **format_datetime(datetime, [format, [timezone]])**
  
  Formats `datetime` as text according to the given `format`.

  If `format` is not specified then the environment's default format is used. The format string can consist of the following characters. Characters like *space*, `:`, `,`, `T`, `-` and `_` are ignored. Any other character is an error.

  * `YY` - last two digits of year 0-99
  * `YYYY` - four digits of year 0000-9999
  * `M` - month 1-12
  * `MM` - month 01-12
  * `D` - day of month, 1-31
  * `DD` - day of month, zero padded 0-31
  * `h` - hour of the day 1-12
  * `hh` - hour of the day 01-12
  * `tt` - twenty four hour of the day 01-23
  * `m` - minute 0-59
  * `mm` - minute 00-59
  * `s` - second 0-59
  * `ss` - second 00-59
  * `fff` - milliseconds
  * `ffffff` - microseconds
  * `fffffffff` - nanoseconds
  * `aa` - am or pm
  * `AA` - AM or PM
  * `Z` - hour and minute offset from UTC, or Z for UTC
  * `ZZZ` - hour and minute offset from UTC

  Timezone should be a location name as specified in the IANA Time Zone database, such as â€œAmerica/Guayaquilâ€ or â€œAmerica/Los_Angelesâ€. If not specified, the current timezone will be used. An error will be returned if the timezone is not recognized.
```objectivec
@(format_datetime("1979-07-18T15:00:00.000000Z")) â†’ 18-07-1979 10:00
@(format_datetime("1979-07-18T15:00:00.000000Z", "YYYY-MM-DD")) â†’ 1979-07-18
@(format_datetime("2010-05-10T19:50:00.000000Z", "YYYY M DD tt:mm")) â†’ 2010 5 10 14:50
@(format_datetime("2010-05-10T19:50:00.000000Z", "YYYY-MM-DD tt:mm AA", "America/Los_Angeles")) â†’ 2010-05-10 12:50 PM
@(format_datetime("1979-07-18T15:00:00.000000Z", "YYYY")) â†’ 1979
@(format_datetime("1979-07-18T15:00:00.000000Z", "M")) â†’ 7
@(format_datetime("NOT DATE", "YYYY-MM-DD")) â†’ ERROR
```
### **format_location(location)**
  
  Formats the given `location` as its name.
```objectivec
@(format_location("Rwanda")) â†’ Rwanda
@(format_location("Rwanda > Kigali")) â†’ Kigali
```
### **format_number(number, places, [humanize])**
  
  Formats `number` to the given number of decimal `places`.

  An optional third argument `humanize` can be false to disable the use of thousand separators.

```objectivec
@(format_number(1234)) â†’ 1,234
@(format_number(1234.5670)) â†’ 1,234.567
@(format_number(1234.5670, 2, true)) â†’ 1,234.57
@(format_number(1234.5678, 0, false)) â†’ 1235
@(format_number("foo", 2, false)) â†’ ERROR
```
### **format_time(time, [format])**
  
  Formats `time` as text according to the given `format`.

  If `format` is not specified then the environment's default format is used. The format string can consist of the following characters. Characters like *space*, `:`, `,`, `T`, `-` and `_` are ignored. Any other character is an error.

  * `h` - hour of the day 1-12
  * `hh` - hour of the day 01-12
  * `tt` - twenty four hour of the day 01-23
  * `m` - minute 0-59
  * `mm` - minute 00-59
  * `s` - second 0-59
  * `ss` - second 00-59
  * `fff` - milliseconds
  * `ffffff` - microseconds
  * `fffffffff` - nanoseconds
  * `aa` - am or pm
  * `AA` - AM or PM
  * `Z` - hour and minute offset from UTC, or Z for UTC
  * `ZZZ` - hour and minute offset from UTC

```objectivec
@(format_time("14:50:30.000000")) â†’ 14:50
@(format_time("14:50:30.000000", "h:mm aa")) â†’ 2:50 pm
@(format_time("15:00:27.000000", "s")) â†’ 27
@(format_time("NOT TIME", "hh:mm")) â†’ ERROR
```
### **format_urn(urn)**
  
  Formats `urn` into human friendly text.
```objectivec
@(format_urn("tel:+250781234567")) â†’ 0781 234 567
@(format_urn("twitter:134252511151#billy_bob")) â†’ billy_bob
@(format_urn(contact.urn)) â†’ (206) 555-1212
@(format_urn(urns.tel)) â†’ (206) 555-1212
@(format_urn(urns.mailto)) â†’ foo@bar.com
@(format_urn("NOT URN")) â†’ ERROR
```
### **if(test, value1, value2)**
  
  Returns `value1` if `test` is truthy or `value2` if not.

  If the first argument is an error that error is returned.


```objectivec
@(if(1 = 1, "foo", "bar")) â†’ foo
@(if("foo" > "bar", "foo", "bar")) â†’ ERROR
```
### **is_error(value)**
  
  Returns whether `value` is an error.
```objectivec
@(is_error(datetime("foo"))) â†’ true
@(is_error(run.not.existing)) â†’ true
@(is_error("hello")) â†’ false
```
### **join(array, separator)**
  
  Joins the given `array` of strings with `separator` to make text.
```objectivec
@(join(array("a", "b", "c"), "|")) â†’ a|b|c
@(join(split("a.b.c", "."), " ")) â†’ a b c
```
### **json(value)**
  
  Returns the JSON representation of `value`.
```objectivec
@(json("string")) â†’ "string"
@(json(10)) â†’ 10
@(json(null)) â†’ null
@(json(contact.uuid)) â†’ "5d76d86b-3bb9-4d5a-b822-c9d86f5d8e4f"
```
### **lower(text)**
  
  Converts `text` to lowercase.
```objectivec
@(lower("HellO")) â†’ hello
@(lower("hello")) â†’ hello
@(lower("123")) â†’ 123
@(lower("ğŸ˜€")) â†’ ğŸ˜€
```
### **max(numbers...)**
  
  Returns the maximum value in `numbers`.
```objectivec
@(max(1, 2)) â†’ 2
@(max(1, -1, 10)) â†’ 10
@(max(1, 10, "foo")) â†’ ERROR
```
### **mean(numbers...)**
  
  Returns the arithmetic mean of `numbers`.
```objectivec
@(mean(1, 2)) â†’ 1.5
@(mean(1, 2, 6)) â†’ 3
@(mean(1, "foo")) â†’ ERROR
```
### **min(numbers...)**
  
  Returns the minimum value in `numbers`.
```objectivec
@(min(1, 2)) â†’ 1
@(min(2, 2, -10)) â†’ -10
@(min(1, 2, "foo")) â†’ ERROR
```
### **mod(dividend, divisor)**
  
  Returns the remainder of the division of `dividend` by `divisor`.
```objectivec
@(mod(5, 2)) â†’ 1
@(mod(4, 2)) â†’ 0
@(mod(5, "foo")) â†’ ERROR
```
### **now()**
  
  Returns the current date and time in the current timezone.
```objectivec
@(now()) â†’ 2019-11-29T20:39:33.370404+02:00
```
### **number(value)**
  
  Tries to convert `value` to a number.

  An error is returned if the value can't be converted.
```objectivec
@(number(10)) â†’ 10
@(number("123.45000")) â†’ 123.45
@(number("what?")) â†’ ERROR
```
### **object(pairs...)**
  
  Takes property name value pairs and returns them as a new object.
```objectivec
@(object()) â†’ {}
@(object("a", 123, "b", "hello")) â†’ {a: 123, b: hello}
@(object("a")) â†’ ERROR
```
### **or(values...)**
  
  Returns whether if any of the given `values` are truthy.
```objectivec
@(or(true)) â†’ true
@(or(true, false, true)) â†’ true
```
### **parse_datetime(text, format, [timezone])**
  
  Parses `text` into a date using the given `format`.

  The format string can consist of the following characters. Characters like *space*, `:`, `,`, `T`, `-` and `_` are ignored. Any other character is an error.

  * `YY` - last two digits of year 0-99
  * `YYYY` - four digits of year 0000-9999
  * `M` - month 1-12
  * `MM` - month 01-12
  * `D` - day of month, 1-31
  * `DD` - day of month, zero padded 0-31
  * `h` - hour of the day 1-12
  * `hh` - hour of the day 01-12
  * `tt` - twenty four hour of the day 01-23
  * `m` - minute 0-59
  * `mm` - minute 00-59
  * `s` - second 0-59
  * `ss` - second 00-59
  * `fff` - milliseconds
  * `ffffff` - microseconds
  * `fffffffff` - nanoseconds
  * `aa` - am or pm
  * `AA` - AM or PM
  * `Z` - hour and minute offset from UTC, or Z for UTC
  * `ZZZ` - hour and minute offset from UTC

  Timezone should be a location name as specified in the IANA Time Zone database, such as â€œAmerica/Guayaquilâ€ or â€œAmerica/Los_Angelesâ€. If not specified, the current timezone will be used. An error will be returned if the timezone is not recognized.

  Note that fractional seconds will be parsed even without an explicit format identifier. You should only specify fractional seconds when you want to assert the number of places in the input format.

  `parse_datetime` will return an error if it is unable to convert the text to a datetime.
```objectivec
@(parse_datetime("1979-07-18", "YYYY-MM-DD")) â†’ 1979-07-18T00:00:00.000000-05:00
@(parse_datetime("2010 5 10", "YYYY M DD")) â†’ 2010-05-10T00:00:00.000000-05:00
@(parse_datetime("2010 5 10 12:50", "YYYY M DD tt:mm", "America/Los_Angeles")) â†’ 2010-05-10T12:50:00.000000-07:00
@(parse_datetime("NOT DATE", "YYYY-MM-DD")) â†’ ERROR
```
### **parse_json(text)**
  
  Tries to parse `text` as JSON.

  If the given `text` is not valid JSON, then an error is returned.
```objectivec
@(parse_json("{\"foo\": \"bar\"}").foo) â†’ bar
@(parse_json("[1,2,3,4]")[2]) â†’ 3
@(parse_json("invalid json")) â†’ ERROR
```
### **parse_time(text, format)**
  
  Parses `text` into `time` using the given `format`.

  The format string can consist of the following characters. Characters like *space*, `:`, `,`, `T`, `-` and `_` are ignored. Any other character is an error.

  * `h` - hour of the day 1-12
  * `hh` - hour of the day 01-12
  * `tt` - twenty four hour of the day 01-23
  * `m` - minute 0-59
  * `mm` - minute 00-59
  * `s` - second 0-59
  * `ss` - second 00-59
  * `fff` - milliseconds
  * `ffffff` - microseconds
  * `fffffffff` - nanoseconds
  * `aa` - am or pm
  * `AA` - AM or PM
  * `Z` - hour and minute offset from UTC, or Z for UTC
  * `ZZZ` - hour and minute offset from UTC
  
  Note that fractional seconds will be parsed even without an explicit format identifier. You should only specify fractional seconds when you want to assert the number of places in the input format.

  `parse_time` will return an error if it is unable to convert the text to a time.
```objectivec
@(parse_time("15:28", "tt:mm")) â†’ 15:28:00.000000
@(parse_time("2:40 pm", "h:mm aa")) â†’ 14:40:00.000000
@(parse_time("NOT TIME", "tt:mm")) â†’ ERROR
```
### **percent(number)**
  
  Formats `number` as a percentage.
```objectivec
@(percent(0.54234)) â†’ 54%
@(percent(1.2)) â†’ 120%
@(percent("foo")) â†’ ERROR
```
### **rand()**
  
  Returns a single random number between [0.0-1.0].
```objectivec
@(rand()) â†’ 0.3849275689214193274523267973563633859157562255859375
@(rand()) â†’ 0.607552015674623913099594574305228888988494873046875
```
### **rand_between()**
  
  A single random integer in the given inclusive range.
```objectivec
@(rand_between(1, 10)) â†’ 5
@(rand_between(1, 10)) â†’ 10
```
### **read_chars(text)**
  
  Converts `text` into something that can be read by IVR systems.

  ReadChars will split the numbers such as they are easier to understand. This includes splitting in 3s or 4s if appropriate.
```objectivec
@(read_chars("1234")) â†’ 1 2 3 4
@(read_chars("abc")) â†’ a b c
@(read_chars("abcdef")) â†’ a b c , d e f
```
### **regex_match(text, pattern, [group])**
  
  Returns the first match of the regular expression `pattern` in `text`.

  An optional third parameter `group` determines which matching group will be returned.
```objectivec
@(regex_match("sda34dfddg67", "\d+")) â†’ 34
@(regex_match("Bob Smith", "(\w+) (\w+)", 1)) â†’ Bob
@(regex_match("Bob Smith", "(\w+) (\w+)", 2)) â†’ Smith
@(regex_match("Bob Smith", "(\w+) (\w+)", 5)) â†’ ERROR
@(regex_match("abc", "[\.")) â†’ ERROR
```
### **remove_first_word(text)**
  
  Removes the first word of `text`.
```objectivec
@(remove_first_word("foo bar")) â†’ bar
@(remove_first_word("Hi there. I'm a flow!")) â†’ there. I'm a flow!
```
### **repeat(text, count)**
  
  Returns `text` repeated `count` number of times.
```objectivec
@(repeat("*", 8)) â†’ ********
@(repeat("*", "foo")) â†’ ERROR
```
### **replace(text, needle, replacement, [count])**
  
  Replaces up to `count` occurrences of `needle` with `replacement` in `text`.

  If `count` is omitted or is less than 0 then all occurrences are replaced.
```objectivec
@(replace("foo bar foo", "foo", "zap")) â†’ zap bar zap
@(replace("foo bar foo", "foo", "zap", 1)) â†’ zap bar foo
@(replace("foo bar", "baz", "zap")) â†’ foo bar
```
### **replace_time(datetime)**
  
  Returns a new datetime with the time part replaced by the `time`.
```objectivec
@(replace_time(now(), "10:30")) â†’ 2018-04-11T10:30:00.000000-05:00
@(replace_time("2017-01-15", "10:30")) â†’ 2017-01-15T10:30:00.000000-05:00
@(replace_time("foo", "10:30")) â†’ ERROR
```
### **round(number, [places])**
  
  Rounds `number` to the nearest value.

  You can optionally pass in the number of decimal places to round to as `places`. If `places < 0`, it will round the integer part to the nearest `10^(-places)`.
```objectivec
@(round(12)) â†’ 12
@(round(12.141)) â†’ 12
@(round(12.6)) â†’ 13
@(round(12.141, 2)) â†’ 12.14
@(round(12.146, 2)) â†’ 12.15
@(round(12.146, -1)) â†’ 10
@(round("notnum", 2)) â†’ ERROR
```
### **round_down(number, [places])**
  
  Rounds `number` down to the nearest integer value.

  You can optionally pass in the number of decimal places to round to as `places`.
```objectivec
@(round_down(12)) â†’ 12
@(round_down(12.141)) â†’ 12
@(round_down(12.6)) â†’ 12
@(round_down(12.141, 2)) â†’ 12.14
@(round_down(12.146, 2)) â†’ 12.14
@(round_down("foo")) â†’ ERROR
```
### **round_up(number, [places])**
  
  Rounds `number` up to the nearest integer value.

  You can optionally pass in the number of decimal places to round to as `places`.
```objectivec
@(round_up(12)) â†’ 12
@(round_up(12.141)) â†’ 13
@(round_up(12.6)) â†’ 13
@(round_up(12.141, 2)) â†’ 12.15
@(round_up(12.146, 2)) â†’ 12.15
@(round_up("foo")) â†’ ERROR
```
### **split(text, delimiters)**
  
  Splits `text` based on the given characters in `delimiters`.

  Empty values are removed from the returned list.
```objectivec
@(split("a b c", " ")) â†’ [a, b, c]
@(split("a", " ")) â†’ [a]
@(split("abc..d", ".")) â†’ [abc, d]
@(split("a.b.c.", ".")) â†’ [a, b, c]
@(split("a|b,c  d", " .|,")) â†’ [a, b, c, d]
```
### **text(value)**
  
  Tries to convert `value` to text.

  An error is returned if the value canâ€™t be converted.
```objectivec
@(text(3 = 3)) â†’ true
@(json(text(123.45))) â†’ "123.45"
@(text(1 / 0)) â†’ ERROR
```
### **text_compare(text1, text2)**
  
  Returns the dictionary order of `text1` and `text2`.

  The return value will be -1 if `text1` comes before `text2`, 0 if they are equal and 1 if `text1` comes after `text2`.
```objectivec
@(text_compare("abc", "abc")) â†’ 0
@(text_compare("abc", "def")) â†’ -1
@(text_compare("zzz", "aaa")) â†’ 1
```
### **text_length(value)**
  
  Returns the length (number of characters) of `value` when converted to text.
```objectivec
@(text_length("abc")) â†’ 3
@(text_length(array(2, 3))) â†’ 6
```
### **text_slice(text, start, [end])**
  
  Returns the portion of `text` between `start` (inclusive) and `end` (exclusive).

  If `end` is not specified then the entire rest of `text` will be included. Negative values for `start` or `end` start at the end of `text`.
```objectivec
@(text_slice("hello", 2)) â†’ llo
@(text_slice("hello", 1, 3)) â†’ el
@(text_slice("helloğŸ˜", -3, -1)) â†’ lo
@(text_slice("hello", 7)) â†’
```
### **time(value)**
  
  Tries to convert `value` to a time.

  If it is text then it will be parsed into a time using the default time format. An error is returned if the value canâ€™t be converted.
```objectivec
@(time("10:30")) â†’ 10:30:00.000000
@(time("10:30:45 PM")) â†’ 22:30:45.000000
@(time(datetime("1979-07-18T10:30:45.123456Z"))) â†’ 10:30:45.123456
@(time("what?")) â†’ ERROR
```
### **time_from_parts(hour, minute, second)**
  
  Creates a time from `hour`, `minute` and `second`.
```objectivec
@(time_from_parts(14, 40, 15)) â†’ 14:40:15.000000
@(time_from_parts(8, 10, 0)) â†’ 08:10:00.000000
@(time_from_parts(25, 0, 0)) â†’ ERROR
```
### **title(text)**
  
  Capitalizes each word in `text`.
```objectivec
@(title("foo")) â†’ Foo
@(title("ryan lewis")) â†’ Ryan Lewis
@(title("RYAN LEWIS")) â†’ Ryan Lewis
@(title(123)) â†’ 123
```
### **today()**
  
  Returns the current date in the environment timezone.
```objectivec
@(today()) â†’ 2019-11-29
```
### **tz(date)**
  
  Returns the name of the timezone of `date`.

  If no timezone information is present in the date, then the current timezone will be returned.
```objectivec
@(tz("2017-01-15T02:15:18.123456Z")) â†’ UTC
@(tz("2017-01-15 02:15:18PM")) â†’ America/Guayaquil
@(tz("2017-01-15")) â†’ America/Guayaquil
@(tz("foo")) â†’ ERROR
```
### **tz_offset(date)**
  
  Returns the offset of the timezone of `date`.

  The offset is returned in the format `[+/-]HH:MM`. If no timezone information is present in the date, then the current timezone offset will be returned.
```objectivec
@(tz_offset("2017-01-15T02:15:18.123456Z")) â†’ +0000
@(tz_offset("2017-01-15 02:15:18PM")) â†’ -0500
@(tz_offset("2017-01-15")) â†’ -0500
@(tz_offset("foo")) â†’ ERROR
```
### **upper(text)**
  
  Converts `text` to lowercase.
```objectivec
@(upper("Asdf")) â†’ ASDF
@(upper(123)) â†’ 123
```
### **url_encode(text)**
  
  Encodes `text` for use as a URL parameter.
```objectivec
@(url_encode("two & words")) â†’ two%20%26%20words
@(url_encode(10)) â†’ 10
```
### **urn_parts(urn)**
  
  Parses a URN into its different parts.
```objectivec
@(urn_parts("tel:+593979012345")) â†’ {display: , path: +593979012345, scheme: tel}
@(urn_parts("twitterid:3263621177#bobby")) â†’ {display: bobby, path: 3263621177, scheme: twitterid}
@(urn_parts("not a urn")) â†’ ERROR
```
### **week_number(date)**

  Returns the week number (1-54) of `date`.

  The week is considered to start on Sunday and Jan 1st is week number 1.

```objectivec
@(week_number("2019-01-01")) â†’ 1
@(week_number("2019-07-23T16:56:59.000000Z")) â†’ 30
@(week_number("xx")) â†’ ERROR
```
### **weekday(date)**
  
  Returns the day of the week for `date`.

  The week is considered to start on Sunday so a Sunday returns 0, a Monday returns 1 etc.
```objectivec
@(weekday("2017-01-15")) â†’ 0
@(weekday("foo")) â†’ ERROR
```
### **word(text, index, [delimiters])**
  
  Returns the word at `index` in `text`.

  Indexes start at zero. There is an optional final parameter `delimiters` which is string of characters used to split the text into words.
```objectivec
@(word("bee cat dog", 0)) â†’ bee
@(word("bee.cat,dog", 0)) â†’ bee
@(word("bee.cat,dog", 1)) â†’ cat
@(word("bee.cat,dog", 2)) â†’ dog
@(word("bee.cat,dog", -1)) â†’ dog
@(word("bee.cat,dog", -2)) â†’ cat
@(word("bee.*cat,dog", 1, ".*=|")) â†’ cat,dog
@(word("O'Grady O'Flaggerty", 1, " ")) â†’ O'Flaggerty
```
### **word_count(text, [delimiters])**
  
  Returns the number of words in `text`.

  There is an optional final parameter delimiters which is string of characters used to split the text into words.
```objectivec
@(word_count("foo bar")) â†’ 2
@(word_count(10)) â†’ 1
@(word_count("")) â†’ 0
@(word_count("ğŸ˜€ğŸ˜ƒğŸ˜„ğŸ˜")) â†’ 4
@(word_count("bee.*cat,dog", ".*=|")) â†’ 2
@(word_count("O'Grady O'Flaggerty", " ")) â†’ 2
```
### **word_slice(text, start, end, [delimiters])**
  
  Extracts a sub-sequence of words from `text`.

  The returned words are those from `start` up to but not-including `end`. Indexes start at zero and a negative end value means that all words after the start should be returned. There is an optional final parameter `delimiters` which is string of characters used to split the text into words.
```objectivec
@(word_slice("bee cat dog", 0, 1)) â†’ bee
@(word_slice("bee cat dog", 0, 2)) â†’ bee cat
@(word_slice("bee cat dog", 1, -1)) â†’ cat dog
@(word_slice("bee cat dog", 1)) â†’ cat dog
@(word_slice("bee cat dog", 2, 3)) â†’ dog
@(word_slice("bee cat dog", 3, 10)) â†’
@(word_slice("bee.*cat,dog", 1, -1, ".*=|,")) â†’ cat dog
@(word_slice("O'Grady O'Flaggerty", 1, 2, " ")) â†’ O'Flaggerty
```